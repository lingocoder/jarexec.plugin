plugins {
    id 'groovy'
    id 'java-gradle-plugin'    
    id 'maven-publish'
    id 'idea'
    id "org.jetbrains.gradle.plugin.idea-ext" version "0.5"
    id 'com.gradle.build-scan' version '2.3'
    id 'com.github.hierynomus.license-base' version '0.15.0'

}

group = 'com.lingocoder'
version = '0.4.9'
description = 'A Gradle plugin that executes Java Jar files'

apply from: 'gradle/sourcesets/custom.sourcesets.gradle'

gradlePlugin {
    plugins {
        jarexecPlugin {
            id = 'com.lingocoder.jarexec'
            implementationClass = 'com.lingocoder.plugin.jarexec.JarExecPlugin'
        }
    }

    testSourceSets sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest
}

repositories{
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies{

    /* JDK 9+ sources may have dependencies on JDK 8- sources in the same project.   */
    /* This next line configures that dependency. This way, we avoid recompiling the */
    /* JDK 8- soures if it's not necessary. */
    
    java9Implementation files(sourceSets.main.output.classesDirs) { builtBy compileJava }
    java11Implementation files(sourceSets.main.output.classesDirs) { builtBy compileJava }
    
    /* The java-gradle-plugin apparently configures the gradle API dependencies only for the         */
    /* default main source set. This next line is needed to explictly set it for JDK 9+ compilation. */

    java9Implementation gradleApi()
    java11Implementation gradleApi()

    java9Implementation group: 'com.lingocoder', name: 'lingocoder.core', version: '0.4.9'
    java11Implementation group: 'com.lingocoder', name: 'lingocoder.core', version: '0.4.9'

    implementation group: 'com.lingocoder', name: 'lingocoder.core', version: '0.4.9'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha4'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.codehaus.groovy', name: 'groovy', version: '3.0.0-alpha-4'
    testImplementation(group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5') {
        exclude module: 'org.codehaus.groovy'
    }
    
    testRuntimeOnly group: 'org.raml.jaxrs', name: 'raml-to-jaxrs-cli', classifier: 'jar-with-dependencies', version: '3.0.5'
}

compileJava {
   sourceCompatibility = 8
   targetCompatibility = 8
}

compileJava9Java {
   sourceCompatibility = 9
   targetCompatibility = 9
}

compileJava11Java {
   sourceCompatibility = 11
   targetCompatibility = 11
}

ext.moduleName = 'com.lingocoder.jarexec'

jar {
   into('META-INF/versions/9') {
      from sourceSets.java9.output
   }
   into('META-INF/versions/11') {
      from sourceSets.java11.output
   }
   inputs.property("moduleName", moduleName)
   manifest.attributes(
      'Multi-Release': 'true',
      'Automatic-Module-Name': moduleName
   )
}

publishing {
    repositories {
        maven {
            url '../consuming/maven-repo'
        }
        ivy {
            url '../consuming/ivy-repo'
        }
    }
}

task fixIdeaPluginClasspath {
    doFirst {
        configure(tasks.pluginUnderTestMetadata) {
            def ideaClassesPath = project.buildDir.toPath().resolveSibling("out").resolve("production")
            def newClasspath = pluginClasspath as List
            newClasspath.add(0, ideaClassesPath)
            pluginClasspath.setFrom(newClasspath)
        }
    }
}

pluginUnderTestMetadata.mustRunAfter(fixIdeaPluginClasspath)

idea.project.settings {
    taskTriggers {
        beforeBuild fixIdeaPluginClasspath, pluginUnderTestMetadata
    }
}

license { header = file('src/main/resources/META-INF/LICENSE-HEADER.txt') }

license {
    excludes(["**/*.txt", "**/*keep*.*", "**/*.gradle*", "**/*.*ignore", "**/*.properties"])

    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'lingocoder'
    ext.email = 'plugins@lingocoder.com'

    skipExistingHeaders = true
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}
