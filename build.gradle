plugins {
    id 'groovy'
    id 'java-gradle-plugin'    
    id 'maven-publish'
    id 'idea'
    id "org.jetbrains.gradle.plugin.idea-ext" version "0.5"
    id 'com.gradle.build-scan' version '2.2.1'
}

group = 'com.lingocoder'
version = '0.1'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

apply from: 'gradle/sourcesets/custom.sourcesets.gradle'

gradlePlugin {
    plugins {
        jarexecPlugin {
            id = 'com.lingocoder.jarexec'
            implementationClass = 'com.lingocoder.plugin.jarexec.JarExecPlugin'
        }
    }
}

repositories{
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies{
    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.3.0-alpha4'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha4'
        
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.codehaus.groovy', name: 'groovy', version: '3.0.0-alpha-4'
    testImplementation(group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5') {
        exclude module: 'org.codehaus.groovy'
    }
    
    testRuntimeOnly group: 'org.raml.jaxrs', name: 'raml-to-jaxrs-cli', classifier: 'jar-with-dependencies', version: '3.0.5'
}

gradlePlugin{
  testSourceSets sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest
}

publishing {
    repositories {
        maven {
            url '../consuming/maven-repo'
        }
        ivy {
            url '../consuming/ivy-repo'
        }
    }
}

task fixIdeaPluginClasspath {
    doFirst {
        configure(tasks.pluginUnderTestMetadata) {
            def ideaClassesPath = project.buildDir.toPath().resolveSibling("out").resolve("production")
            def newClasspath = pluginClasspath as List
            newClasspath.add(0, ideaClassesPath)
            pluginClasspath.setFrom(newClasspath)
        }
    }
}

pluginUnderTestMetadata.mustRunAfter(fixIdeaPluginClasspath)

idea.project.settings {
    taskTriggers {
        beforeBuild fixIdeaPluginClasspath, pluginUnderTestMetadata
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

